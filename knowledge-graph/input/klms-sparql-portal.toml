title="KLMS SPARQL endpoint"

[[tabGroups]]

name="Dataset information"

[[tabGroups.tabs]]

name="Recently published datasets"
query="""
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?dataset ?res ?name ?date ?path ?size WHERE {
  ?dataset dcat:distribution ?res .
  ?res dct:title ?name .
  ?res dct:issued ?date .
  ?res dcat:accessURL ?path .
  ?res dcat:byteSize ?size .
  FILTER (?date >= "2024-01-01T00:00:01"^^xsd:dateTime)
} ORDER BY DESC(?date)
"""

[[tabGroups.tabs]]

name="Datasets published by an organization"
query="""
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?dataset ?title ?date WHERE {
  ?dataset dct:publisher ?pub .
  ?pub foaf:name "Athena Research Center" .
  ?dataset dct:title ?title .
  ?dataset dct:issued ?date 
}
"""

[[tabGroups.tabs]]

name="Datasets in spatial area of interest"
query="""
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX klms: <http://stelar-project.eu/klms#>
PREFIX geo:<http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
SELECT ?dataset ?date_modified ?bbox WHERE {
  ?dataset dct:spatial ?loc .
  ?dataset dct:issued ?date_issued .
  ?dataset dct:modified ?date_modified .
  ?loc dcat:bbox ?bbox .
  FILTER ((geof:sfWithin(?bbox, "POLYGON((10.5 42.3,10.5 45.9,12.8 45.9,12.8 42.3,10.5 42.3))"^^geo:wktLiteral)))
} 
"""

[[tabGroups]]

name="Workflow & Tasks"

[[tabGroups.tabs]]
name="Workflow executions"
query="""
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX klms: <http://stelar-project.eu/klms#>
SELECT ?id ?creator ?state ?start_date ?end_date ?tag ?tag_value WHERE {
  ?workflowExec a klms:WorkflowExecution .
  ?workflowExec dct:identifier ?id .
  OPTIONAL { ?workflowExec dct:creator ?creator . }
  ?workflowExec klms:state ?state .
  ?workflowExec dcat:startDate ?start_date .
  OPTIONAL { ?workflowExec dcat:endDate ?end_date .}
  OPTIONAL {
    ?workflowExec klms:hasTags ?kvpair . 
    ?kvpair klms:key ?tag .
    ?kvpair klms:value ?tag_value .
  }
}
"""

[[tabGroups.tabs]]
name="Tasks executed in workflow"
query="""
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX klms: <http://stelar-project.eu/klms#>
SELECT ?task_id ?state ?start_date ?end_date ?tag ?tag_value WHERE {
  ?workflowExec a klms:WorkflowExecution .
  ?workflowExec dct:identifier "a30d7527-0007-4e70-bcb7-46de3faaa83c" .
  ?taskExec dct:isPartOf ?workflowExec .
  ?taskExec klms:state ?state .
  ?taskExec dct:identifier ?task_id .
  ?taskExec dcat:startDate ?start_date .
  OPTIONAL { ?taskExec dcat:endDate ?end_date .}
  OPTIONAL {
    ?taskExec klms:hasTags ?kvpair . 
    ?kvpair klms:key ?tag .
    ?kvpair klms:value ?tag_value .
  }
} ORDER BY ?start_date
"""


[[tabGroups.tabs]]
name="Task execution input datasets"
query="""
PREFIX dcat: <http://www.w3.org/ns/dcat#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX klms: <http://stelar-project.eu/klms#> 
SELECT  ?input_uri ?order ?resource_id WHERE {
  ?taskExec dct:identifier "2dfb891f-a9d4-48ca-849a-55af3dd28858" . 
  ?taskExec klms:hasInput ?input_uri . 
  ?input_uri klms:orderNum ?order . 
  ?input_uri klms:input ?resource . 
  ?resource dct:identifier ?resource_id 
} ORDER BY ?order
"""

[[tabGroups.tabs]]
name="Task execution output datasets"
query="""
PREFIX dcat: <http://www.w3.org/ns/dcat#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX klms: <http://stelar-project.eu/klms#> 
SELECT ?output_uri ?order ?resource_id WHERE {
  ?taskExec dct:identifier "2dfb891f-a9d4-48ca-849a-55af3dd28858" . 
  ?taskExec klms:hasOutput ?output_uri . 
  ?output_uri klms:orderNum ?order . 
  ?output_uri klms:output ?resource . 
  ?resource dct:identifier ?resource_id 
} ORDER BY ?order
"""

[[tabGroups.tabs]]
name="Task execution metrics"
query="""
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX klms: <http://stelar-project.eu/klms#> 
SELECT ?metric ?value ?timestamp WHERE { 
 ?taskExec dct:identifier "02e9127b-602f-45c0-bc51-5c3629e5420e" .
 ?taskExec klms:hasMetrics ?kvpair .
 ?kvpair  klms:key ?metric.
 ?kvpair klms:value ?value .
 ?kvpair dct:issued ?timestamp 
}
"""

[[tabGroups.tabs]]
name="Task execution parameters"
query="""
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX klms: <http://stelar-project.eu/klms#> 
SELECT ?parameter ?value WHERE { 
 ?taskExec dct:identifier "02e9127b-602f-45c0-bc51-5c3629e5420e" .
 ?taskExec klms:hasParameters ?kvpair .
 ?kvpair  klms:key ?parameter .
 ?kvpair klms:value ?value . 
}
"""
